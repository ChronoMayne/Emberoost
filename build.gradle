buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'net.minecraftforge.gradle'

version = "$project.mc_version-$project.mod_version"
group = 'com.github.wolfShotz.wyrmroost'
archivesBaseName = "$project.archiveBaseName"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

sourceSets {
    main.resources.srcDirs += 'src/main/generated_resources'
}

minecraft {
    mappings channel: 'official', version: "$mc_version"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            args '--username', 'Dev###'

            mods {
                wyrmroost {
                    sources sourceSets.main
                }
            }
        }

        client2 {
            parent(null, "client")
            args '--username', 'Dev2'
        }

        server {
            workingDirectory project.file('run')

            mods {
                wyrmroost {
                    sources sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            args '--mod', 'wyrmroost', '--server', '--client', '--dev', '--reports', '--validate', '--output', file('src/main/generated_resources/'), '--existing', file('src/main/resources/')

            mods {
                wyrmroost {
                    sources sourceSets.main
                    sources sourceSets.test
                }
            }
        }
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "Wyrmroost",
                "Specification-Vendor"    : "WolfShotz",
                "Specification-Version"   : 1,
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "WolfShotz",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
repositories {
    maven {
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        url "https://cursemaven.com"
    }
}

dependencies {
    // Specify the version of Minecraft to use, If this is any group other then 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency. And it's patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft "net.minecraftforge:forge:$mc_version-$forge_version"

    implementation fg.deobf('software.bernie.geckolib:geckolib-forge-1.16.5:3.0.45')

    annotationProcessor 'org.spongepowered:mixin:0.8:processor'
}

jar.finalizedBy('reobfJar')